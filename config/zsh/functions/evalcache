# Caches the output of a binary initialization command, to avoid the time to
# execute it in the future.
#
# Usage: _evalcache <command> <generation args...>
# Source: https://github.com/mroth/evalcache/blob/master/evalcache.plugin.zsh
# Version: 2022-06-15
# Placed here since I don't have a plugin system in use
function _evalcache () {
  local cmdHash="nohash"

  if builtin command -v md5 > /dev/null; then
    cmdHash=$(echo -n "$*" | md5)
  elif builtin command -v md5sum > /dev/null; then
    cmdHash=$(echo -n "$*" | md5sum | cut -d' ' -f1)
  fi

  local cacheFile="$ZSH_EVALCACHE_DIR/init-${1##*/}-${cmdHash}.sh"

  if [ "$ZSH_EVALCACHE_DISABLE" = "true" ]; then
    eval "$("$@")"
  elif [ -s "$cacheFile" ]; then
    source "$cacheFile"
  else
    if type "$1" > /dev/null; then
      (>&2 echo "$1 initialization not cached, caching output of: $*")
      mkdir -p "$ZSH_EVALCACHE_DIR"
      "$@" > "$cacheFile"
      source "$cacheFile"
    else
      echo "evalcache ERROR: $1 is not installed or in PATH"
    fi
  fi
}
